buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:semver-vcs-gradle-grgit:0.1.0'
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.sonarqube' version '2.2.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

apply plugin: 'org.ajoberstar.semver-vcs-grgit'
apply from: 'gradle/compatTest.gradle'

group = 'org.ajoberstar'

sonarqube {
    properties {
        property 'sonar.projectVersion', version.toString().split('-')[0]
    }
}

repositories {
    jcenter()
}

configurations.all {
    exclude group: 'org.codehaus.groovy'
}

dependencies {
    compile gradleApi()

    compatTestCompile gradleTestKit()
    compatTestCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.outputs.files
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = 'bintray'
            url = 'https://api.bintray.com/maven/ajoberstar/maven/gradle-bintray/;publish=1'
            credentials {
                username = System.env['BINTRAY_USER']
                password = System.env['BINTRAY_KEY']
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/gradle-bintray'
    vcsUrl = 'https://github.com/ajoberstar/gradle-bintray.git'
    description = 'Simpler publishing to Bintray'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.bintray'
            displayName = 'Simple Bintray Publish Plugin'
            tags = ['publish', 'bintray']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

wrapper {
    gradleVersion = '3.2.1'
}
